import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.3'

    defaultConfig {
        applicationId "com.vdopiaadsdemo"
        minSdkVersion 16
        targetSdkVersion PROP_TARGET_SDK_VERSION
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        ndk {
            abiFilters "armeabi-v7a", "x86", "armeabi", "mips"
        }
        externalNativeBuild {
            ndkBuild {
                if (!project.hasProperty("PROP_NDK_MODE") || PROP_NDK_MODE.compareTo('none') != 0) {
                    // skip the NDK Build step if PROP_NDK_MODE is none
                    targets 'MyGame'
                    arguments 'NDK_TOOLCHAIN_VERSION=4.9'
                    arguments 'APP_PLATFORM=android-' + PROP_TARGET_SDK_VERSION

                    def module_paths = [project.file("../../cocos2d").absolutePath,
                                        project.file("../../cocos2d/cocos").absolutePath,
                                        project.file("../../cocos2d/external").absolutePath]
                    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                        //should use '/'
                        module_paths = module_paths.collect { it.replaceAll('\\\\', '/') }
                        arguments 'NDK_MODULE_PATH=' + module_paths.join(";")
                    } else {
                        arguments 'NDK_MODULE_PATH=' + module_paths.join(':')
                    }

                    arguments '-j' + Runtime.runtime.availableProcessors()
                    abiFilters.addAll(PROP_APP_ABI.split(':').collect { it as String })
                }
            }
        }
    }

    sourceSets.main {
        java.srcDir "src"
        res.srcDir "res"
        manifest.srcFile "AndroidManifest.xml"
        assets.srcDir "../../Resources"
    }

    externalNativeBuild {
        ndkBuild {
            if (!project.hasProperty("PROP_NDK_MODE") || PROP_NDK_MODE.compareTo('none') != 0) {
                // skip the NDK Build step if PROP_NDK_MODE is none
                path "jni/Android.mk"
            }
        }
    }

    signingConfigs {

        release {
            if (project.hasProperty("RELEASE_STORE_FILE")) {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (project.hasProperty("RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.release
            }

            externalNativeBuild {
                ndkBuild {
                    arguments 'NDK_DEBUG=0'
                }
            }
        }

        debug {
            externalNativeBuild {
                ndkBuild {
                    arguments 'NDK_DEBUG=1'
                }
            }
        }
    }
    repositories {
        jcenter()
        maven { url 'https://jitpack.io' }
        maven { url "https://adcolony.bintray.com/AdColony" }
        maven { url "https://dl.bintray.com/vdopiacorp/sdk" }
        maven { url 'https://maven.google.com' }
    }
    repositories {
        flatDir {
            dirs 'libs'
        }
    }
}
dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':libcocos2dx')
    compile 'com.vdopia.ads.lw:coresdk:2.5.2.5'
    compile("com.vdopia.ads.lw:cocos2dx-plugin:1.0.3")
    compile 'com.google.android.gms:play-services-ads:11.8.0'
    compile 'com.android.support:multidex:1.0.2'
    compile 'com.vdopia.ads.lw:adcolony-adapter:1.0.4'
    compile 'com.vdopia.ads.lw:applovin-adapter:1.0.4'
    compile 'com.vdopia.ads.lw:chartboost-adapter:1.0.4'
    compile 'com.vdopia.ads.lw:facebook-adapter:1.0.4'
    compile 'com.vdopia.ads.lw:google-adapter:1.0.4'
    compile 'com.vdopia.ads.lw:inmobi-adapter:1.0.4'
    compile 'com.vdopia.ads.lw:mopub-adapter:1.0.4'
    compile 'com.vdopia.ads.lw:tapjoy-adapter:1.0.4'
    compile 'com.vdopia.ads.lw:unity-adapter:1.0.4'
    compile 'com.vdopia.ads.lw:vungle-adapter:1.0.4'
    compile 'com.vdopia.ads.lw:yahoo-adapter:1.0.4'
    compile 'com.devbrackets.android:exomedia:4.0.3'
    compile 'com.squareup.picasso:picasso:2.5.2'
}
configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                //You can use your own preferred version of Android Support Library
                details.useVersion('26.0.2')
            }
        }
    }
}